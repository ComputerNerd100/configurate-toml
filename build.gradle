plugins {
    id 'java'
    id 'maven'
}

group 'me.lucko.configurate'
version '3.7'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.spongepowered:configurate-core:3.7'
    compile 'com.moandjiezana.toml:toml4j:0.7.2'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

// Only used occasionally for deployment - not needed for normal builds.

apply plugin: 'signing'
javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'https://docs.oracle.com/javase/8/docs/api/'
    )
    options.addStringOption('Xdoclint:none', '-quiet')
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}
task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
signing {
    useGpgCmd()
    sign configurations.archives
}
uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            //repository(url: 'https://nexus.lucko.me/repository/maven-snapshots/') {
            //    authentication(userName: luckoNexusUsername, password: luckoNexusPassword)
            //}
            pom {
                project {
                    name 'configurate-toml'
                    description 'configurate loader for TOML'
                    url 'https://github.com/lucko/configurate-toml'
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id 'lucko'
                            name 'Luck'
                            url 'https://lucko.me'
                            email 'git@lucko.me'
                        }
                    }
                    scm {
                        connection 'scm:git:https://github.com/lucko/configurate-toml.git'
                        developerConnection 'scm:git:git@github.com:lucko/configurate-toml.git'
                        url 'https://github.com/lucko/configurate-toml'
                    }
                    issueManagement {
                        system 'GitHub'
                        url 'https://github.com/lucko/configurate-toml/issues'
                    }
                }
            }
        }
    }
}

